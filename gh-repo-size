#!/usr/bin/env -S nu --stdin
# Query the size of select GitHub repository

def main [
    repo: string,           # [owner/repo] or URL
    --raw (-r)              # Request via bare API (lower rate limit)
]: any -> filesize {
    # parse string
    let repo_path: string = (
        $repo | str replace --regex '^(https?://)?github\.com/|/+$' ''
    )

    # validate
    if ($repo_path !~ '/') {
        error make {msg: "Usage: gh repo-size owner/repo"}
    }

    # get data
    let size_kb: int = try {
        if $raw {
            # c.f. curl+jq
            (http get $"https://api.github.com/repos/($repo_path)").size
        } else {
            # github-cli
            (gh api $"repos/($repo_path)" | from json).size
        }
    } catch {
        error make {
            msg: $"Error: '($repo_path)' not found or API rate limited."
        }
    }

    # format
    ($size_kb * 1kb) | into filesize
}
